apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply from: 'external-dependencies.gradle'
apply from: 'idea-config.gradle'

group 'verifiable-log'
version '0.1-SNAPSHOT'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile junit, quicktheories, mockito
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams true
        showCauses true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

jar.baseName = "verifiable-log"

jar {
    doFirst {
        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
        manifest {
            attributes(
                    "Main-Class": "uk.gov.verifiablelog.Application",
                    "Class-Path": configurations.runtime.findAll { !it.directory }.collect { it.name }.join(' ')
            )
        }
    }
}
