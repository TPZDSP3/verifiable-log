plugins {
    id 'com.jfrog.bintray' version '1.6'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply from: 'external-dependencies.gradle'
apply from: 'idea-config.gradle'

def majorMinorVersion = '0.2'
def buildNumber = System.getenv('TRAVIS_BUILD_NUMBER') ? System.getenv('TRAVIS_BUILD_NUMBER') : '0000'
def versionNumber = majorMinorVersion + '.' + buildNumber

group 'verifiable-log'
version versionNumber

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile junit, quicktheories, mockito
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams true
        showCauses true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

jar.baseName = "verifiable-log"

jar {
    doFirst {
        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
        manifest {
            attributes(
                    "Class-Path": configurations.runtime.findAll { !it.directory }.collect { it.name }.join(' ')
            )
        }
    }
}

publishing {
    publications {
        JarPublication(MavenPublication) {
            from components.java
            groupId 'verifiable-log'
            artifactId 'verifiable-log'
            version versionNumber
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_PASS')
    publications = ['JarPublication']
    publish = true
    pkg {
        repo = 'openregister'
        name = 'verifiable-log'
        userOrg = 'openregister'
        licenses = 'MIT'
        version {
            name = versionNumber
        }
    }
}
